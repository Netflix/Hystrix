apply plugin: 'osgi'

configurations {
    ajtools
    ajcTestCompile.extendsFrom testCompile
    ajcTestRuntime.extendsFrom testRuntime
    ajc
}


sourceSets {
    ajcTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }

    ajc {
        def main = sourceSets.main
        java.srcDirs = main.java.srcDirs
        resources.srcDirs = main.resources.srcDirs
        compileClasspath = main.compileClasspath
        runtimeClasspath = main.runtimeClasspath
    }
}



compileAjcTestJava {

    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"

    doLast {
        ant.taskdef(resource: "org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties", classpath: configurations.ajtools.asPath)
        ant.iajc(source: "1.6", target: "1.6",
                destDir: "${sourceSets.ajcTest.output.classesDir.absolutePath}", maxmem: "512m", fork: "true",
                classpath: "${sourceSets.main.output.classesDir.absolutePath};${configurations.testCompile.asPath}")
                {
                    sourceroots {
                        files("src/ajcTest/java/com/netflix/hystrix/contrib/javanica/test/aspectj", "src/test/java/com/netflix/hystrix/contrib/javanica/test/common", "src/main/java/com/netflix/hystrix/contrib/javanica/aop/aspectj").each {
                            File file -> pathelement(location: file.absolutePath)
                        }
                    }
                }
    }

}

compileAjcJava {
    sourceCompatibility = "1.6"
    targetCompatibility = "1.6"

    doLast {
        ant.taskdef(resource: "org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties", classpath: configurations.ajtools.asPath)
        ant.iajc(source: "${sourceCompatibility}", target: "${targetCompatibility}",
                destDir: "${sourceSets.ajc.output.classesDir.absolutePath}", maxmem: "512m", fork: "true", "showWeaveInfo": "true",
                classpath: "${configurations.compile.asPath}")

                {
                    sourceroots {
                        sourceSets.ajc.java.srcDirs.each {
                            pathelement(location: it.absolutePath)

                        }
                    }
                }
    }
}

task ajcTest(type: Test) {
    testClassesDir = sourceSets.ajcTest.output.classesDir
    classpath = sourceSets.ajcTest.runtimeClasspath
    if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-Xdebug',
                '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=9009'
    }
}

check.dependsOn test, ajcTest


ext {
    aspectjVersion = '1.8.6'
    springframeworkVesion = '4.3.2.RELEASE'
}


task ajcJar(type: Jar) {
    archiveName = "${project.name}" + "-ctw-${version}.jar"
    destinationDir = file("${buildDir}/libs")
    from sourceSets.ajc.output
}

assemble.dependsOn(jar, ajcJar)

dependencies {
    compileApi project(':hystrix-core')
    ajtools "org.aspectj:aspectjtools:$aspectjVersion"
    testRuntime "org.aspectj:aspectjrt:$aspectjVersion"
    compileApi "org.aspectj:aspectjweaver:$aspectjVersion"
    compile "org.aspectj:aspectjrt:$aspectjVersion"

    compileApi 'com.google.guava:guava:15.0'
    compile 'org.apache.commons:commons-lang3:3.1'
    compileApi 'com.google.code.findbugs:jsr305:2.0.0'
    compile 'org.ow2.asm:asm:5.0.4'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'pl.pragmatists:JUnitParams:1.0.5'
    testCompile project(':hystrix-junit')
    testCompile "org.springframework:spring-core:$springframeworkVesion"
    testCompile "org.springframework:spring-context:$springframeworkVesion"
    testCompile "org.springframework:spring-aop:$springframeworkVesion"
    testCompile "org.springframework:spring-test:$springframeworkVesion"
    testCompile 'cglib:cglib:3.1'
    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'log4j:log4j:1.2.17'
    testCompile 'org.slf4j:slf4j-log4j12:1.7.7'
    testCompile 'com.tngtech.java:junit-dataprovider:1.10.2'
}
