configurations {
    ajtools
    ajcTestImplementation.extendsFrom testCompileClasspath
    ajcTestRuntimeOnly.extendsFrom testRuntimeClasspath
    ajc
}


sourceSets {
    ajcTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }

    ajc {
        def main = sourceSets.main
        java.srcDirs = main.java.srcDirs
        resources.srcDirs = main.resources.srcDirs
        compileClasspath = main.compileClasspath
        runtimeClasspath = main.runtimeClasspath
    }
}



compileAjcTestJava {

    doLast {
        ant.taskdef(resource: "org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties", classpath: configurations.ajtools.asPath)
        ant.iajc(source: "1.8", target: "1.8",
                destDir: "${sourceSets.ajcTest.output.classesDirs.files.first().absolutePath}", maxmem: "512m", fork: "true",
                classpath: "${sourceSets.main.output.classesDirs.files.first()absolutePath};${configurations.testCompileClasspath.asPath}")
                {
                    sourceroots {
                        files("src/ajcTest/java/com/netflix/hystrix/contrib/javanica/test/aspectj", "src/test/java/com/netflix/hystrix/contrib/javanica/test/common", "src/main/java/com/netflix/hystrix/contrib/javanica/aop/aspectj").each {
                            File file -> pathelement(location: file.absolutePath)
                        }
                    }
                }
    }

}

compileAjcJava {

    doLast {
        ant.taskdef(resource: "org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties", classpath: configurations.ajtools.asPath)
        ant.iajc(source: "${sourceCompatibility}", target: "${targetCompatibility}",
                destDir: "${sourceSets.ajc.output.classesDirs.files.first().absolutePath}", maxmem: "512m", fork: "true", "showWeaveInfo": "true",
                classpath: "${configurations.compileClasspath.asPath}")

                {
                    sourceroots {
                        sourceSets.ajc.java.srcDirs.each {
                            pathelement(location: it.absolutePath)

                        }
                    }
                }
    }
}

task ajcTest(type: Test) {
    testClassesDirs = sourceSets.ajcTest.output.classesDirs
    classpath = sourceSets.ajcTest.runtimeClasspath
    if (System.getProperty('DEBUG', 'false') == 'true') {
        jvmArgs '-Xdebug',
                '-Xrunjdwp:transport=dt_socket,server=y,suspend=y,address=9009'
    }
}

check.dependsOn test, ajcTest


ext {
    aspectjVersion = '1.8.6'
    springframeworkVesion = '4.3.2.RELEASE'
}


task ajcJar(type: Jar) {
    archiveFileName = "${project.name}" + "-ctw-${version}.jar"
    destinationDirectory = file("${buildDir}/libs")
    from sourceSets.ajc.output
}

assemble.dependsOn(jar, ajcJar)

dependencies {
    api project(':hystrix-core')
    ajtools "org.aspectj:aspectjtools:$aspectjVersion"
    testRuntimeOnly "org.aspectj:aspectjrt:$aspectjVersion"
    api "org.aspectj:aspectjweaver:$aspectjVersion"
    implementation "org.aspectj:aspectjrt:$aspectjVersion"

    api 'com.google.guava:guava:15.0'
    implementation 'org.apache.commons:commons-lang3:3.1'
    api 'com.google.code.findbugs:jsr305:2.0.0'
    implementation 'org.ow2.asm:asm:5.0.4'
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'pl.pragmatists:JUnitParams:1.0.5'
    testImplementation project(':hystrix-junit')
    testImplementation "org.springframework:spring-core:$springframeworkVesion"
    testImplementation "org.springframework:spring-context:$springframeworkVesion"
    testImplementation "org.springframework:spring-aop:$springframeworkVesion"
    testImplementation "org.springframework:spring-test:$springframeworkVesion"
    testImplementation 'cglib:cglib:3.1'
    testImplementation 'org.mockito:mockito-all:1.9.5'
    testImplementation 'log4j:log4j:1.2.17'
    testImplementation 'org.slf4j:slf4j-log4j12:1.7.7'
    testImplementation 'com.tngtech.java:junit-dataprovider:1.10.2'
}
